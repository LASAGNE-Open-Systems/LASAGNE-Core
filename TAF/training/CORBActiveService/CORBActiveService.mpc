// Simplified single vendor MPC (without specific vendor mpb's)

project(CORBActiveService_Export) {
  custom_only = 1

  prebuild      = perl $(ACE_ROOT)/bin/generate_export_file.pl CORBActiveService > CORBActiveService_export.h
}


project(CORBActiveService) : taflib, CORBActiveService {

  requires	+= opendds

  sharedname    = *

  idlflags      += -Wb,export_include=CORBActiveService_export.h \
                   -Wb,export_macro=CORBActiveService_Export
  
  libout        = $(DAF_ROOT)/lib 

  dynamicflags  += CORBACTIVESERVICE_BUILD_DLL

  after         += CORBActiveService_Export

  Idl_Files {
    LTMTopicDetails.idl
    CORBActiveService.idl
  }

  Header_Files {
    CORBActiveService.h
    CORBActiveDataSupport.h
  }

  Source_Files {
    CORBActiveService.cpp
    CORBActiveDataSupport.cpp
  }
}

// Multi-vendor MPC (with specific vendor mpb's)

project(ODDS_CORBActiveService) : ODDSCORBActiveService, odds_pubsub {
  requires += opendds
  after    += CORBActiveService
}

project(NDDS_CORBActiveService) : NDDSCORBActiveService, ndds_pubsub {
  requires += ndds
  after    += CORBActiveService ODDS_CORBActiveService

  NDDSTypeSupport_Files {
    LTMTopicDetails.idl
  }
}

project(CDDS_CORBActiveService) : CDDSCORBActiveService, cdds_pubsub {
  requires += coredx
  after    += CORBActiveService ODDS_CORBActiveService NDDS_CORBActiveService

  CoreDXTypeSupport_Files {
    LTMTopicDetails.idl
  }
}

project(OSPL_CORBActiveService) : OSPLCORBActiveService, ospl_pubsub {
  requires += opensplice
  after    += CORBActiveService ODDS_CORBActiveService NDDS_CORBActiveService CDDS_CORBActiveService

  OSPLTypeSupport_Files {
    LTMTopicDetails.idl
  }
}

project(CORBActiveClient) : taflib {
  exename    = *
  after     += CORBActiveService

  macros    += CORBACTIVESERVICE_HAS_DLL=0 

  verbatim(vc14, debug_cmd_args) {
    "-ORBInitRef LTM_CORBActiveService=corbaloc:iiop:localhost:8989/LTM_CORBActiveService -ORBObjRefStyle URL"
  }

  verbatim(vs2017, debug_cmd_args) {
    "-ORBInitRef LTM_CORBActiveService=corbaloc:iiop:localhost:8989/LTM_CORBActiveService -ORBObjRefStyle URL"
  }

  IDL_Files {
  }

  Source_Files {
    CORBActiveClient.cpp
  }
}

